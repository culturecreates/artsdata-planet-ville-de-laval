# [endpoint=https://artsdata-trifid-production.herokuapp.com/query]

PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
select ?uri ?name ?startDate ?endDate  (sample(?location_name) as ?location) ?same_as
where {
    values ?g {
        <http://kg.artsdata.ca/culture-creates/artsdata-planet-ville-de-laval/calendrier-activites>
    }
    graph  ?g {
        ?uri a schema:Event .
    }
    ?uri a schema:Event ;
         schema:startDate ?startDate ;
         schema:name ?name  ;
         schema:location ?location .
    ?location schema:address/schema:postalCode ?code ; schema:name ?location_name .
    OPTIONAL {
        ?uri   schema:endDate ?endDate 
    }
    bind(strdt(substr(str(?startDate),1,10),xsd:date) as ?date )
    filter (?startDate  > now() || ?endDate > now() )
    values ?type {
        <https://www.laval.ca/52ff792f-a72e-4b2b-859e-2b9faf17491a> # Bibliothèques
        <https://www.laval.ca/03e3e7d7-ad9a-4f10-86db-dfc9d49b7abb>  # Expositions et spectacles
        <https://www.laval.ca/685b3eae-7097-4a44-8e72-1c769ed8f6cf> # Événements et festivals
        <https://www.laval.ca/1e9e6613-0402-4d1f-b7e9-91a837684b74> # Virée patrimoniale
        <https://www.laval.ca/06591f03-ee66-4c31-89ca-78d35dcea4cc> # Histoire
    }
    ?uri schema:additionalType ?type .
    filter not exists {
        ?focusNode sh:focusNode ?uri . # SHACL errors
    }
    filter not exists {
        ?uri  schema:additionalType <https://www.laval.ca/f5d84cb0-fd36-4751-8992-44022fcb2eaf> # Enseignants
    }
    filter(!contains(?name,"Petits bonheurs"))
    {
        OPTIONAL {
            ?uri schema:sameAs ?same_as .
            filter(STRSTARTS(str(?same_as),"http://kg.artsdata.ca/resource/K"))
        }
        OPTIONAL {
            ?same_as schema:sameAs+ ?uri .
            filter(STRSTARTS(str(?same_as),"http://kg.artsdata.ca/resource/K"))
        }
    }
} group by ?uri ?name  ?startDate ?endDate  ?same_as
order by ?startDate ?endDate
