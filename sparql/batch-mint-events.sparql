# [endpoint=http://kg.artsdata.ca/query]
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
select distinct ?s ?location_name  where {
    values ?g {
        <http://kg.artsdata.ca/culture-creates/artsdata-planet-ville-de-laval/calendrier-activites>
    }
    graph  ?g {
        ?s a schema:Event .
    }
    ?s a schema:Event ;
       schema:startDate ?startDate ;
       schema:name ?name  ;
       schema:location/schema:sameAs/schema:address/schema:postalCode ?code ;
                                                   schema:location/schema:name ?location_name .
    OPTIONAL {
        ?s   schema:endDate ?endDate 
    }
    bind(strdt(substr(str(?startDate),1,10),xsd:date) as ?date )
    filter (?startDate  > now() || ?endDate > now() )
    values ?type {
        <https://www.laval.ca/52ff792f-a72e-4b2b-859e-2b9faf17491a> # Bibliothèques
        <https://www.laval.ca/03e3e7d7-ad9a-4f10-86db-dfc9d49b7abb>  # Expositions et spectacles
        <https://www.laval.ca/685b3eae-7097-4a44-8e72-1c769ed8f6cf> # Événements et festivals
       
    }
    ?s schema:additionalType ?type .
    filter not exists {
        ?focusNode sh:focusNode ?s .
    }
    filter not exists {
        ?s  schema:additionalType <https://www.laval.ca/f5d84cb0-fd36-4751-8992-44022fcb2eaf> # Enseignants
    }
    FILTER NOT EXISTS {
        graph ?other_g {
            ?event2 a schema:Event ;
                    schema:name ?name2  ;
                    schema:startDate ?startDate2 .
            bind(strdt(substr(str(?startDate2),1,10),xsd:date) as ?date2 )
           
        }
         filter (?date2 = ?date)
            ?event2   schema:location/schema:address/schema:postalCode ?code .
            filter (?s != ?event2)
            filter(?g != ?other_g)
        
    }
} order by ?s